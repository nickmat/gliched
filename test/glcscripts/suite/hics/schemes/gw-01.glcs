// isow-01.glcs - Uppercase iso week scheme

lexicon W {
    name "Weekday names";
    fieldname wday;
    lang en;
    pseudo    Weekday,  WDay;
    tokens {
        1, "Monday",    "Mon";
        2, "Tuesday",   "Tue";
        3, "Wednesday", "Wed";
        4, "Thursday",  "Thur";
        5, "Friday",    "Fri";
        6, "Saturday",  "Sat";
        7, "Sunday",    "Sun";
    }
}

grammar ISOW {
    fields year week day;
    lexicons W;
    alias lexicon { day wday; }
    alias pseudo { d day; ww week; yyyy year; }
    alias unit { d day; y year; w week; }
    format iso { rules iso8601 week; }
    format  ywd,   "{year}| {week}| {day:W:a}";
    format "ywd+", "{year}| {week}| {day:W}";
    preferred ywd;
}

scheme ISOW {
    name "ISO:8601 Standard Week";
    base isoweek;
    grammar ISOW;
}

write nl;
write record.ISOW 2460137 nl;
write record.ISOW:ywd "2023 28 Tue" nl;
write record.ISOW:iso "2023W282" nl;
write text.ISOW:ywd 2460137 nl;
write text.ISOW:ywd {s:ISOW 2023, 28, 2} nl;
write text.ISOW:iso 2460137 nl;
write date.ISOW:ywd "2023 28 Tue" nl;
write date {s:ISOW 2023, 28, 2} nl;

/*[OUTPUT]

{s:ISOW 2023, 28, 2}
{s:ISOW 2023, 28, 2}
{s:ISOW 2023, 28, 2}
2023 28 Tue
2023 28 Tue
2023-W28-2
2460137
2460137

[OUTPUT]*/
